apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = "io.github.galbiston.rdf_tables.Main"
}
mainClassName = "io.github.galbiston.rdf_tables.Main"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    ext.openCSVVersion = "3.9"
    implementation ("com.opencsv:opencsv:$openCSVVersion"){
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    ext.slf4jVersion = "1.7.25"
    implementation "org.slf4j:jcl-over-slf4j:$slf4jVersion"

    ext.jenaVersion = "3.8.0"
    //implementation "org.apache.jena:apache-jena-libs:$jenaVersion"
    //Issue resolving apache-jena-libs in Maven projects. Use individual modules as provide jars.
    implementation "org.apache.jena:jena-core:$jenaVersion"
    implementation "org.apache.jena:jena-arq:$jenaVersion"
    implementation "org.apache.jena:jena-iri:$jenaVersion"
    implementation "org.apache.jena:jena-tdb:$jenaVersion"

    ext.logbackVersion = "1.2.3"
    testCompile "ch.qos.logback:logback-classic:$logbackVersion"

    ext.junitVersion = "4.12"
    testImplementation "junit:junit:$junitVersion"

    ext.jcommanderVersion = "1.72"
    implementation "com.beust:jcommander:$jcommanderVersion"
}

apply plugin: 'maven-publish'
group = 'io.github.galbiston'
version = '1.0.0'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                artifactId = 'rdf-tables'
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'RDF Tables'
                    description = 'Conversion of delimited value/tabular files into RDF serialisation.'
                    url = 'https://github.com/galbiston/rdf-tables'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'Greg Albiston'
                            email = 'galbiston@mail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/galbiston/rdf-tables.git'
                        developerConnection = 'scm:git:ssh://git@github.com:galbiston/rdf-tables.git'
                        url = 'https://github.com/galbiston/rdf-tables.git'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

build.finalizedBy(publishToMavenLocal)

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html4', true)
    }
}

run {
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}
