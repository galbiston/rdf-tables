apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = "io.github.galbiston.rdf_tables.Main"
}
mainClassName = "io.github.galbiston.rdf_tables.Main"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    ext.openCSVVersion = "5.6"
    implementation ("com.opencsv:opencsv:$openCSVVersion"){
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    ext.slf4jVersion = "1.7.36"
    implementation "org.slf4j:jcl-over-slf4j:$slf4jVersion"

    ext.jenaVersion = "4.4.0"
    implementation "org.apache.jena:jena-tdb:$jenaVersion"
    implementation "org.apache.jena:jena-tdb2:$jenaVersion"

    ext.logbackVersion = "1.2.11"
    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"

    ext.junitVersion = "4.13.2"
    testImplementation "junit:junit:$junitVersion"

    ext.jcommanderVersion = "1.82"
    implementation "com.beust:jcommander:$jcommanderVersion"
}

group = 'io.github.galbiston'
version = '1.0.5'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                artifactId = 'rdf-tables'
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'RDF Tables'
                    description = 'Conversion of delimited value/tabular files into RDF serialisation.'
                    url = 'https://github.com/galbiston/rdf-tables'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'Greg Albiston'
                            email = 'galbiston@mail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/galbiston/rdf-tables.git'
                        developerConnection = 'scm:git:ssh://git@github.com:galbiston/rdf-tables.git'
                        url = 'https://github.com/galbiston/rdf-tables.git'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

build.finalizedBy(publishToMavenLocal)

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html4', true)
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

run {
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}
